apply plugin: 'com.android.application'
apply plugin: 'packer'

// gradlew clean apkRelease     打包命令
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 28

    dataBinding {
        enabled = true
    }

    dexOptions {
//        incremental true
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        encoding = "UTF-8"
    }

    defaultConfig {
        applicationId "com.example.myarouter"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
        ndk { abiFilters "armeabi" }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    signingConfigs {
        release {
            storeFile file("myapp.jks")
            storePassword KEYSTORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "_debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.release
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packer {
        archiveNameFormat = '${buildType}-v${versionName}-${channel}'
        archiveOutput = new File(project.rootProject.buildDir, "apks")
        channelFile = new File(project.rootDir, "markets.txt")
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    if (isNeedHomeModule.toBoolean()) {
        implementation project(':module_home')
    }
    if (isNeedChatModule.toBoolean()) {
        implementation project(':module_chat')
    }
    if (isNeedRecomModule.toBoolean()) {
        implementation project(':module_recom')
    }
    if (isNeedMeModule.toBoolean()) {
        implementation project(':module_me')
    }
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
